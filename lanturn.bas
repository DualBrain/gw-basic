1 ' - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
2 ' LANTURN By Neil C. Obremski (Mar 2011)
3 '
10 DEFINT A-Z: KEY OFF: RANDOMIZE TIMER: OPTION BASE 1: SCREEN 7, 1, 1, 0: WIDTH 40: WINDOW: VIEW: CLS
20 DEF FNSS4(W,H) = 4 + FIX(FIX((W * 4 + 7) / 8) * H / 2)
25 DEF FNCOLLIDE(X1,W1,O1,X2,W2,O2) = ABS(X1+(F1*W1)-X2+(F2*W2))
30 DIM WX(5), WY(5), WS(5), WP(5) ' Wolves
40 DIM BX(10), BY(10), BH!(10), BV!(10) ' Blood
100 ' Initialize Keyboard (ESC=QUIT, ARROW KEYS=MOVE)
110   KEY 15, CHR$(0)+CHR$(&H01): KEY(15) ON: ON KEY(15) GOSUB 9990
120   KEY 16, CHR$(0)+CHR$(&H39): KEY(16) ON: ON KEY(16) GOSUB 160
140   KEY(11) ON: KEY(12) ON: KEY(13) ON: KEY(14) ON: ON KEY(11) GOSUB 180: ON KEY(12) GOSUB 181: ON KEY(13) GOSUB 182: ON KEY(14) GOSUB 183
150 GOTO 200
160   BA = BA + 1: RETURN ' 16/39 = SPACE (BUTTON "A")
180   MY = MY - 1: RETURN ' 11 = ARROW UP
181   MX = MX - 1: RETURN ' 12 = ARROW LEFT
182   MX = MX + 1: RETURN ' 13 = ARROW RIGHT
183   MY = MY + 1: RETURN ' 14 = ARROW DOWN
190 ' Keybuffer SUB (i.e. QB arrow key check) -- MEMORY LEAKS w/ STRING CONCAT
191   K$ = INKEY$: IF K$ = "" THEN RETURN
192   IF K$ = CHR$(0) + CHR$(72) THEN MY = MY - 1
193   IF K$ = CHR$(0) + CHR$(75) THEN MX = MX - 1
194   IF K$ = CHR$(0) + CHR$(77) THEN MX = MX + 1
195   IF K$ = CHR$(0) + CHR$(80) THEN MY = MY + 1
196   ' IF K$ = " " THEN BA = BA + 1
198   WHILE INKEY$ <> "": WEND ' clear buffer
199   RETURN
200 ' Initialize Palettes
300 ' Initialize Sprites (P=Player, L=Light, A=Arm, W=Wolf, M=Moon)
310   DIM CTBL(256): GOSUB 7000
320   DIM SP1(FNSS4(32,64)), SA1(FNSS4(64,32)), SL1(FNSS4(96,64)), SM1(FNSS4(32,32)), ST1(FNSS4(32,100))
330   CLS: LINE (0,0) - (31,61), 15, B: LOCATE 2,2: PRINT "P";: LOCATE 3,2: PRINT "L";: LOCATE 4,2: PRINT "A";: LOCATE 5,2: PRINT "Y";: LOCATE 6,2: PRINT "E";: LOCATE 7,2: PRINT "R";: GET (0,0) - (31, 61), SP1
335   CLS: W = 64: H = 32: GOSUB 8000: PUT (0,0), SW1 ' Wolf Sprites
340   CLS: LINE (0,0) - (63,31), 4, B: LOCATE 2,2: PRINT "LANTERN": GET (0,0) - (63,31), SA1
350   CLS: CIRCLE (48,32), 32, 14: PAINT (48,32), 14, 14: GET (0,0) - (95,63), SL1
360   CLS: LINE (0,0) - (31,31), 8, BF: CIRCLE(16,16), 16, 15: PAINT (16,16), 15: GET (0,0) - (31,31), SM1
370   CLS: LINE (10,0) - (10,100), 0: GET (0,0) - (31, 99), ST1
390   CLS: LINE (0,0) - (319,100), 8, BF: PCOPY 1,2
400 ' Reset Game Variables
410   PX = 128: PY = 118: PT! = 0: PV = 0: PL = 100: PF = 1: PR = 0: PH = 100' Player (L=Lantern)
420   MX = 0: MY = 0: BA = 0 ' Movement Trackers
430   L = 0: PC! = 0 ' Level / Percent Complete
440   D = 0 ' Distance Calculator
450   BC = 0: BI = 0 ' Blood Count / Index
460   I = 0: J = 0: X = 0: Y = 0: W = 0: H = 0: A$ = "": C$ = "" ' Registers
490   FOR I = 1 TO 5: WX(I) = 0: WY(I) = (I - 1) * 32: WS(I) = 15: WP(I) = 1: NEXT I
500 ' Title Screen
510   CLS
520   LOCATE 1,1: PRINT "LANTURN "; FRE(0); " -> "; FRE("")
530   PCOPY 1,0
890   MX = 0: MY = 0: BA = 0: WHILE INKEY$ <> "": A$ = INKEY$: WEND
900   ' Wait for ANY key (including arrows)
920   WHILE TIMER < T2: WEND: T1! = TIMER + .0334: T2! = T1! + .0334: A$ = INKEY$
980   IF A$ = "" AND MX = 0 AND MY = 0 AND BA = 0 THEN 900
990   CLS : MX = 0: MY = 0: BA = 0
2000 ' -- MAIN LOOP --
2010   L = L + 1: PC! = L / 1000: PCOPY 1,0: PCOPY 2,1: T1! = TIMER + .0334: T2! = TIMER + .0334
2100   ' PLAYER / LANTERN
2110   IF MX <> 0 THEN PF = SGN(SGN(MX) + 1): I = MX: PX = PX + I: MX = MX - I: IF PX < 32 THEN PX = 32 ELSE IF PX > 256 THEN PX = 256
2120   IF MY <> 0 THEN I = MY: PY = PY + I: MY = MY - I: IF PY < 100 THEN PY = 100 ELSE IF PY > 136 THEN PY = 136
2130   'GOSUB 190 ' For QB
2140   I = BA: IF I > 0 THEN BA = BA - I: PL = PL - 1: PR = 1: PUT (PX - 32 + (32 * PF), PY), SA1 ELSE PR = 0
2150   PUT (PX, PY), SP1
2160   IF PR = 1 THEN PUT (PX - 32, PY), SL1, XOR
2200   ' MOON / SUN
2210   IF L < 1000 THEN PUT (PC! * 288, ABS(SIN(1.57 + (PC! * 3.14))) * 20), SM1, OR
2300   ' WOLVES
2310   FOR I = 1 TO PC! * 5
2320     D = ABS(PX-(WX(I)+(SGN(SGN(WS(I))+1)*32)))
2330     ON WP(I) GOTO 3100, 3200, 3300, 3400, 3500, 3600, 3700
2390   NEXT I
2400   ' BLOOD
2410   FOR BI = 1 TO 10
2420     IF BX(BI) > 0 THEN 4100
2490   NEXT BI
2500   ' TREES
2510   PUT (0,0), ST1, AND
2600   ' ...
2700   ' ...
2800   ' STATUS
2810   LOCATE 25, 1: COLOR 15: PRINT "FUEL: "; PL; " HEALTH: "; PH;
2900   ' Timer Wait
2910   WHILE TIMER < T1!
2920     ' extra time for effects
2930   WEND
2940   WHILE TIMER < T2!: WEND
2990 GOTO 2000
3000 ' WOLF STATE CHANGERS (set WP(I), reset WT(I), and GOTO 2290)
3010  WP(I) = 1: GOTO 2390
3020  WP(I) = 2: GOTO 2390
3030  WP(I) = 3: WS(I) = WS(I) * - 1: GOTO 2390
3040  WP(I) = 4: WS(I) = SGN(WS(I)) * D / 5: GOTO 2390
3100 ' WOLF SUB: INACTIVE ... SPAWN?
3110  IF RND < 0.5 THEN 2390 ' nope
3120  WX(I) = SGN(CINT(RND)) * 256: WY(I) = 100 + FIX(RND * 68): WS(I) = 5 + (RND * 15)
3130  IF WX(I) > 0 THEN WS(I) = WS(I) * -1 ' go left instead of right
3140  WP(I) = 2 ' set purpose to seeking
3150  PUT (WX(I), WY(I)), SW1, OR
3190 ON (SGN(SGN(WS(I))+1)*6) + (L MOD 6) + 1 GOTO 8501, 8502, 8503, 8504, 8505, 8506, 8507, 8508, 8509, 8510, 8511, 8512
3200 ' WOLF SUB: SEEKING
3210  WX(I) = WX(I) + WS(I): IF WX(I) < 0 OR WX(I) > 256 THEN 3010
3220  WY(I) = WY(I) + SGN(PY - WY(I)): IF WY(I) > 168 THEN WY(I) = 168
3230  IF D < 64 AND PR = 1 THEN 3030 ' Run Away!
3240  IF D < 32 AND PR = 0 THEN 3040 ' Attack!
3290 ON (SGN(SGN(WS(I))+1)*6) + (L MOD 6) + 1 GOTO 8501, 8502, 8503, 8504, 8505, 8506, 8507, 8508, 8509, 8510, 8511, 8512
3300 ' WOLF SUB: RUNNING AWAY
3310  WX(I) = WX(I) + WS(I): IF WX(I) < 0 OR WX(I) > 256 THEN 3010
3320  WY(I) = WY(I) + SGN(WY(I) - PY): IF WY(I) > 168 THEN WY(I) = 168
3390 ON (SGN(SGN(WS(I))+1)*6) + (L MOD 6) + 1 GOTO 8501, 8502, 8503, 8504, 8505, 8506, 8507, 8508, 8509, 8510, 8511, 8512
3400 ' WOLF SUB: ATTACKING
3410  IF PR = 1 THEN 3030
3420  IF PH > 0 THEN PH = PH - 1
3430  GOSUB 4000
3490 ON (SGN(SGN(WS(I))+1)*6) + (L MOD 6) + 1 GOTO 8501, 8502, 8503, 8504, 8505, 8506, 8507, 8508, 8509, 8510, 8511, 8512
4000 ' BLOOD SUB: SPAWN
4010  IF BC = 10 THEN RETURN
4020  BC = BC + 1: BI = 0
4030  FOR J = 1 TO 10
4040   IF BX(J) = 0 THEN BI = J: J = 10
4050  NEXT J
4055  PSET (100 + BI, 99), 0
4060  IF BI = 0 THEN SCREEN 0: STOP ' Baaad
4070  BX(BI) = PX + FIX(16 * RND) + 8: BY(BI) = PY + FIX(16 * RND) + 8
4080  BH!(BI) = (9.99 * RND) - 5.01: BV!(BI) = -9.99 * RND
4090 RETURN
4100 ' BLOOD SUB: FALL
4110  PSET (BX(BI), BY(BI)), 4
4120  BX(BI) = BX(BI) + BH!(BI)
4130  BY(BI) = BY(BI) + BV!(BI)
4140  BH!(BI) = BH!(BI) + (-1 * SGN(BH!(BI))): BV!(BI) = BV!(BI) + 1.01
4150  IF BY(BI) > PY + 48 THEN BX(BI) = 0: BC = BC - 1
4190 GOTO 2490
7000 ' SPRITE SUBS (INIT HERE)
7010 CTBL(ASC(" ")) = 0
7020 CTBL(ASC("#")) = 1
7030 CTBL(ASC("R")) = 4
7040 CTBL(ASC(".")) = 7
7050 CTBL(ASC("+")) = 8
7090 RETURN
7100 ' SPRITE SUB: DRAW STANDARD
7110  CLS
7120  FOR Y = 1 TO H
7130   READ A$
7140   FOR X = 1 TO W
7150    PSET (X-1,Y-1), CTBL(ASC(MID$(A$,X,1)))
7160  NEXT X, Y
7190 RETURN
7200 ' SPRITE SUB: DRAW MIRRORED
7210  CLS
7220  FOR Y = 1 TO H
7230   READ A$
7240   FOR X = 1 TO W
7250    PSET (W-X,Y-1), CTBL(ASC(MID$(A$,X,1)))
7260  NEXT X, Y
7290 RETURN
8000 DATA "                                                                "
8001 DATA "                                                                "
8002 DATA "                                                                "
8003 DATA "                                                                "
8004 DATA "                                                                "
8005 DATA "          #.##.+#                                               "
8006 DATA "         ###++.#.####                                           "
8007 DATA "        #.+++..+++...####        ######                         "
8008 DATA "       #+..+++++++.++..++##    ##++++++###  ######              "
8009 DATA "       #.RRRR+...++.+++.##.####++++++++++.##..++++##         #  "
8010 DATA "     ##.RRRR++..+..+++++#...++++++++++++++.#...+++++###     ##  "
8011 DATA "   ##..++++++...+..+++++...+++++++++++++++...##..+++++.#####+#  "
8012 DATA "  ##+..++++.+++..+.++++##.+++...++.++++++....####..+++++++++.## "
8013 DATA "  #+.++..##..++++.++++.#.+++..++++...++++....+#  #...++++++...# "
8014 DATA "   #..##..#........+++++.++...++++...+++++....+#  #..........#  "
8015 DATA "    ##..###..........+.++++....+++....++++.#...+#  #........#   "
8016 DATA "      ###.....#........++++++...+...#.++++..##.#    ##....##    "
8017 DATA "        ####.#..+++##..++.+++++.....#.++.....###      ####      "
8018 DATA "            ###..###..++.+.+.+++#.####.+#.....###               "
8019 DATA "            #.###.#....+...++#..#######..#.######               "
8020 DATA "            #..#...#..#....++.#########...###...#               "
8021 DATA "             #.##...#..#.#.....##########.#####..#              "
8022 DATA "            #++.#.#.######.++..#   #######.###...#              "
8023 DATA "           #++..#.#####  #.+..#    ###### #  ##..#              "
8024 DATA "           #+..####     #.+..#      #####     #..#              "
8025 DATA "           #..###       #++.#         ####    #..#              "
8026 DATA "            #...#       #+.#          ##.#     #..#             "
8027 DATA "             ###        #...#        ##..#     #+.#             "
8028 DATA "                         #..#       #...#     #++.#             "
8029 DATA "                          ##        ####     #+.+.#             "
8030 DATA "                                             #####              "
8031 DATA "                                                                "
8035 DIM SW1(FNSS4(64,32)), SW7(FNSS4(64,32)): RESTORE 8000
8040 GOSUB 7100: GET (0,0)-(63,31), SW1: RESTORE 8000: GOSUB 7200: GET (0,0)-(63,31),SW7
8050 DATA "                                                                "
8051 DATA "              ## ###                                            "
8052 DATA "             ####.+#                                            "
8053 DATA "           ##..+++####                                          "
8054 DATA "         ##+++++..+++.####                                      "
8055 DATA "        #+..+++++++++++..+###                                   "
8056 DATA "       #+.RRRR+.+++.++.....+.##                                 "
8057 DATA "       #.RRRR+....+++++....+++.##    ######                     "
8058 DATA "    ###..++++++..+..++++.++++++++####++++++###  ########        "
8059 DATA "   ##...+++++...++.++..++++++...++++++++..++.###++++++++##      "
8060 DATA "   #.+++++....+++++.+++..++++....++++++++++++.....++++++++###   "
8061 DATA "    #...#.##...++..+.++++++++..++++++++++++++..##...+++++++++## "
8062 DATA "     ##..###........+..+++++...+++++++++++++...####....+++++... "
8063 DATA "       ###.............++++++.+.++++++++++++..+.#  #..........# "
8064 DATA "          ####.....##..+..++++..++++...++++....+#   ##.......## "
8065 DATA "             ##...##...+++.+.+.#.......+++....#.+#    #######   "
8066 DATA "             ##.#.#.#..++.+++.##....#..++++....###              "
8067 DATA "              ###.#.##......++####..##..++......#               "
8068 DATA "            ###.##...##..#.....#######.#+........#              "
8069 DATA "           #+....##.###.###....#########......#.###             "
8070 DATA "           #+..## ########.....#  #######...#..####             "
8071 DATA "          #+.##   ## #  #++...#   #########.#####.#             "
8072 DATA "          #+.#         #++..##     ####### #.# #...#            "
8073 DATA "         ##..#        #...##        ######  #   #..#            "
8074 DATA "         #..#         #...#           #####      #.#            "
8075 DATA "         ###           #...#           ##.#      #..#           "
8076 DATA "                        ###             #.#      #+.#           "
8077 DATA "                                       #..#      #..#           "
8078 DATA "                                      #..#      #++.#           "
8079 DATA "                                      ####      #++.#           "
8080 DATA "                                                ####            "
8081 DATA "                                                                "
8085 DIM SW2(FNSS4(64,32)), SW8(FNSS4(64,32)): RESTORE 8050
8090 GOSUB 7100: GET (0,0)-(63,31), SW2: RESTORE 8050: GOSUB 7200: GET (0,0)-(63,31),SW8
8100 DATA "                                                                "
8101 DATA "                    ###                                         "
8102 DATA "        ##    ######.+######                                    "
8103 DATA "       #+#  ##.+#...+..++++.###    #####                        "
8104 DATA "      #.#####++#...+++.+++++++.####.+++.##                      "
8105 DATA "      ##..+++.....++.++....++++++++++..++.###########           "
8106 DATA "     #.+++++++++++..++++.+..++++++++++++++.#.++++++++###        "
8107 DATA "     #++++++...+......+++...+++++++++++++++.......+++++.##      "
8108 DATA "    #.++.RR++...++++....++...++++++++++++++..###....+++++.#     "
8109 DATA "    #.+.RRR++..++.++++.++++++++++++++++++....#####....++++.#    "
8110 DATA "    #.+RRR++++++++++...++++.+++++++++++++++...+#  ##.....++##   "
8111 DATA "  ##.+++++++..++..+++.++++++..+++++.+++++......+#   #.....+.#   "
8112 DATA " ##..+++++++++++++....+..+++.#.++....+++++.......#   ##....+.#  "
8113 DATA " #..++++.##...+++.....++.+++.#........+++++.....#      ###.+##  "
8114 DATA "  #++.##.#....+........++...+###..##...+++.+....##        #.#   "
8115 DATA "   ####...........##.#.+++...########....++......##        ##   "
8116 DATA "       ##..##...##.###...+..#   ######.....#..#..#          #   "
8117 DATA "        ####.#.######......#     #######.#..###..##             "
8118 DATA "       ############........#      #########.####...#            "
8119 DATA "     ###...###   #+++...###        ####### ## #....#            "
8120 DATA "    #....###    #++..###            ##..#      ##.+.#           "
8121 DATA "   #+...##     #++.##                 ##.##      #++.#          "
8122 DATA "  #++..#       #++.#                   #..#       #++#          "
8123 DATA " #++.##        #...#                    #+#       #++#          "
8124 DATA "#++.#           #..#                     ##        #+#          "
8125 DATA "####             ##                                 ##          "
8126 DATA "                                                                "
8127 DATA "                                                                "
8128 DATA "                                                                "
8129 DATA "                                                                "
8130 DATA "                                                                "
8131 DATA "                                                                "
8135 DIM SW3(FNSS4(64,32)), SW9(FNSS4(64,32)): RESTORE 8100
8140 GOSUB 7100: GET (0,0)-(63,31), SW3: RESTORE 8100: GOSUB 7200: GET (0,0)-(63,31),SW9
8150 DATA "                                                                "
8151 DATA "                                              ##++++++##        "
8152 DATA "                               ######      ###+++++++++.#       "
8153 DATA "                   ## ###   ###.++++.######.++++++....+++##     "
8154 DATA "       ##    ## # #+##++####.++++..++..#.++++++.........++#     "
8155 DATA "      #.#   #.####++.++..++++++++++.++...............#####.#    "
8156 DATA "      #.####++#.#++..++++++++++++++++++.###############   ##    "
8157 DATA "      ##+++++.++..+..++++++++++++++++++..#                 #    "
8158 DATA "     #.++++++++++...+++.++++++++++++++...##                     "
8159 DATA "     #++++++..++.+++++...+++++++.+++++....#                     "
8160 DATA "    #.++.RR++.++++++.+++..+++++..++++++...##                    "
8161 DATA "    #++.RRR++..++++.+++++.+++++..+++++++...#                    "
8162 DATA "    #++RRR+++++++++..++..++.++...++++++...#                     "
8163 DATA "  ##.+++++++..++...+.+++.+........++++.....#                    "
8164 DATA " ##..+++++++++++....#+.+++.....##.++.++.#.#                     "
8165 DATA " #..+++++##.+++++....++++..######..+...+.###                    "
8166 DATA "  #++.##.#...+++...#.++.+..#######........##                    "
8167 DATA "   ####...........##......#########......####                   "
8168 DATA "     #####......#.#+...###     ######.#.#...##                  "
8169 DATA "         ###.#.###.++..###      ########......##                "
8170 DATA "         #..######++..##         ###########....#               "
8171 DATA "         #...#  #++..#               ##..#  ##.+#               "
8172 DATA "         #..#   #+..#                  #+#   #++#               "
8173 DATA "        #...#  #++.#                    ##    #.#               "
8174 DATA "        #..#   #++#                            ##               "
8175 DATA "        #..#  #+++#                                             "
8176 DATA "       #..#   #+..#                                             "
8177 DATA "      #.+.#   #..#                                              "
8178 DATA "     ##+.#    ###                                               "
8179 DATA "    #+++.#                                                      "
8180 DATA "    #####                                                       "
8181 DATA "                                                                "
8185 DIM SW4(FNSS4(64,32)), SW10(FNSS4(64,32)): RESTORE 8150
8190 GOSUB 7100: GET (0,0)-(63,31), SW4: RESTORE 8150: GOSUB 7200: GET (0,0)-(63,31),SW10
8200 DATA "                                                       #####+#  "
8201 DATA "                             ########          ########+++++.#  "
8202 DATA "                           ##.++++++.##     ###.+++++++++++.#   "
8203 DATA "              ##    ## ####.+++++...++.#####.++++++++++++..#    "
8204 DATA "         ##  #+#  ##+##+#.+++++++++.+++...+++++++++++.....###   "
8205 DATA "        #.###++###++#.++.+++++++++++++..................###     "
8206 DATA "        ##++++#.+++.++++++++++++++++++...#####.......###        "
8207 DATA "       #+++++.++++.+++++++++++++++++++.....#  #######           "
8208 DATA "      #++..++..++...+++.+++++++++++++++...#                     "
8209 DATA "      #+.RRR.+...++.++...++++++..++++++..#                      "
8210 DATA "      #+RRR+++++++++++..++++++...+++++++..##                    "
8211 DATA "    ##++RR++++..++++++++++......#.++++.+....#                   "
8212 DATA "  ##.+++++++++...++..+++.++##...#.+++......##                   "
8213 DATA "  #..+++++.++++++++..+++..+.######.+......##                    "
8214 DATA "   #......##++...++++++++.++.######........#                    "
8215 DATA "    #####.#......++++.++.++..########......#                    "
8216 DATA "       #...........++++++.+##  ########.....#                   "
8217 DATA "        ####.........++++..+#   ##### ###...#                   "
8218 DATA "            ###........++...#    ###     #..#                   "
8219 DATA "             ##....#......##    ####     #..#                   "
8220 DATA "              ####.###....#     ###     #..#                    "
8221 DATA "                ##..##..##     #..#     #+.#                    "
8222 DATA "                  ####...##    ###     #++.#                    "
8223 DATA "                    ##+..#.#           #++#                     "
8224 DATA "                     #+.#..#           ###                      "
8225 DATA "                    #++.##.#                                    "
8226 DATA "                    #+.#  ##                                    "
8227 DATA "                  ##++.#                                        "
8228 DATA "                 #+++.#                                         "
8229 DATA "                #+++.#                                          "
8230 DATA "                #####                                           "
8231 DATA "                                                                "
8235 DIM SW5(FNSS4(64,32)), SW11(FNSS4(64,32)): RESTORE 8200
8240 GOSUB 7100: GET (0,0)-(63,31), SW5: RESTORE 8200: GOSUB 7200: GET (0,0)-(63,31),SW11
8250 DATA "                                                                "
8251 DATA "                                                                "
8252 DATA "          ##  ####                                              "
8253 DATA "         #.###+++######  ###                                    "
8254 DATA "         ##++++#.+++.#.##++###   ######                         "
8255 DATA "        #++++...+++.#...+++...###.+++++##    #######            "
8256 DATA "       #++++++++++++++++++.+++++++++++++.####+++++++####      # "
8257 DATA "       #+.RRR+++++++++++++++.++++++++++++...++++++++++++####### "
8258 DATA "      ##.RRR++++..++++++++++++++++++++++++.......+++++++++++++# "
8259 DATA "    ###.+++++++..++..++.++++++++++++++++++..####....+++++++..#  "
8260 DATA "   ##..+++++++++++++...++..+++++++++++++....#   ##............# "
8261 DATA "   #+.++++....+++...++++..++++++++++++++....#     #.........##  "
8262 DATA "    #......#..+++....+++++++++++.++++++++...#      ##.....##    "
8263 DATA "     ##...##....++....+++++++++..+++++++.+..#        #####      "
8264 DATA "       ###.........+++++++++......++++++..++.#                  "
8265 DATA "          ###.......++..++.+++..#..+++.+....##                  "
8266 DATA "             ##......++.+++.++..##...+.....##                   "
8267 DATA "              ###....++++++.++.#####........#                   "
8268 DATA "               #####..+.+++++++#####......#..#                  "
8269 DATA "               ######....+++++++######.....##                   "
8270 DATA "                ######.....++++###..####...#                    "
8271 DATA "                #########...+.+#..####.....#                    "
8272 DATA "                 ####### ##....#####..+..##                     "
8273 DATA "                 ######    ##...###++..##                       "
8274 DATA "                  ####       #..###..##                         "
8275 DATA "                             #..#####                           "
8276 DATA "                              #+.#                              "
8277 DATA "                              #++#                              "
8278 DATA "                             #.++#                              "
8279 DATA "                            #++.#                               "
8280 DATA "                            #####                               "
8281 DATA "                                                                "
8285 DIM SW6(FNSS4(64,32)), SW12(FNSS4(64,32)): RESTORE 8250
8290 GOSUB 7100: GET (0,0)-(63,31), SW6: RESTORE 8250: GOSUB 7200: GET (0,0)-(63,31),SW12
8300 RETURN
8500 ' PUT helpers
8501 PUT (WX(I), WY(I)), SW1, OR: GOTO 2390
8502 PUT (WX(I), WY(I)), SW2, OR: GOTO 2390
8503 PUT (WX(I), WY(I)), SW3, OR: GOTO 2390
8504 PUT (WX(I), WY(I)), SW4, OR: GOTO 2390
8505 PUT (WX(I), WY(I)), SW5, OR: GOTO 2390
8506 PUT (WX(I), WY(I)), SW6, OR: GOTO 2390
8507 PUT (WX(I), WY(I)), SW7, OR: GOTO 2390
8508 PUT (WX(I), WY(I)), SW8, OR: GOTO 2390
8509 PUT (WX(I), WY(I)), SW9, OR: GOTO 2390
8510 PUT (WX(I), WY(I)), SW10, OR: GOTO 2390
8511 PUT (WX(I), WY(I)), SW11, OR: GOTO 2390
8512 PUT (WX(I), WY(I)), SW12, OR: GOTO 2390
8990 RETURN
9990 CLS : SCREEN 0, 0, 0, 0: WIDTH 80: CLS : END

